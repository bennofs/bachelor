\chapter{Appendix}

SPARQL-Abfrage zum Test des Timeouts (terminiert für Limit 2500, verursacht ein Timeout bei Limit 3000):

\begin{lstlisting}[language=SPARQL, caption={SPARQL-Query für Person-Items (verwendet NamedSubqueries, eine SPARQL-Erweiterung von BlazeGraph)}, label={lst:sparql-persons}]
CONSTRUCT {
  ?node ?p ?o .
}

# get items
WITH {
  SELECT ?item WHERE {
   # instances of class human
   ?item wdt:P31 wd:Q5 . 
  } LIMIT 3000
} AS %items

# get statements for items
WITH {
  SELECT ?stmt WHERE {
    INCLUDE %items .
    ?item ?p ?stmt .
    [] wikibase:claim ?p .
  }
} AS %stmts

# get references for statements
WITH {
  SELECT REDUCED ?ref WHERE {
    INCLUDE %stmts .
    ?stmt prov:wasDerivedFrom ?ref .
  }
} AS %refs 

# get values for statements/references
WITH {
  SELECT REDUCED ?val WHERE {
    {
      INCLUDE %stmts .
      ?stmt ?p ?val .
      [] wikibase:statementValue|wikibase:statementValueNormalized|
         wikibase:referenceValue|wikibase:referenceValueNormalized|
         wikibase:qualifierValue|wikibase:qualifierValueNormalized 
       ?p .
      FILTER (STRSTARTS(STR(?val), "http://www.wikidata.org/value/"))
    }
    UNION
    {
      INCLUDE %refs .
      ?ref ?p ?val .
      [] wikibase:statementValue|wikibase:statementValueNormalized|
         wikibase:referenceValue|wikibase:referenceValueNormalized|
         wikibase:qualifierValue|wikibase:qualifierValueNormalized 
       ?p .
      FILTER (STRSTARTS(STR(?val), "http://www.wikidata.org/value/"))
    }
  }
} AS %vals 

# get all nodes
WITH {
  SELECT ?node WHERE {
    { SELECT (?item as ?node) WHERE { INCLUDE %items } }
    UNION 
    { SELECT (?stmt as ?node) WHERE { INCLUDE %stmts } } 
    UNION
    { SELECT (?ref as ?node) WHERE { INCLUDE %refs } } 
    UNION
    { SELECT (?val as ?node) WHERE { INCLUDE %vals } }
  }
} as %nodes

WHERE {
  INCLUDE %nodes .
  ?node ?p ?o .
}
\end{lstlisting}